plugins {
    id("myboot.starter-conventions")
    id("myboot.publish-conventions")
}

dependencies {

    api("com.github.fmjsjx:libcommon-rocketmq")
    api("org.apache.rocketmq:rocketmq-client") {
        exclude(group = "io.netty", module = "netty-all")
        exclude(group = "io.netty", module = "netty-tcnative-boringssl-static")
    }
    api("org.apache.rocketmq:rocketmq-acl")
    api("io.netty:netty-handler")
    
    implementation(group = "io.netty", name = "netty-transport-native-epoll", classifier = "linux-x86_64")
    implementation(group = "io.netty", name = "netty-transport-native-kqueue", classifier = "osx-x86_64")
    implementation(group = "io.netty", name = "netty-tcnative-boringssl-static", classifier = "linux-aarch_64")
    implementation(group = "io.netty", name = "netty-tcnative-boringssl-static", classifier = "linux-x86_64")
    implementation(group = "io.netty", name = "netty-tcnative-boringssl-static", classifier = "osx-x86_64")
    implementation(group = "io.netty", name = "netty-tcnative-boringssl-static", classifier = "windows-x86_64")

}

description = "MyBoot/Starter RocketMQ"

publishing {
    publications {
    	create<MavenPublication>("mavenJava") {
        	from(components["java"])
        	versionMapping {
                usage("java-api") {
                    fromResolutionOf("runtimeClasspath")
                }
                usage("java-runtime") {
                    fromResolutionResult()
                }
            }
            pom {
                name.set("MyBoot/Starter RocketMQ")
                description.set("A boot library provides some additional extensions based on SpringBoot.")
                url.set("https://github.com/fmjsjx/myboot")
                licenses {
                    license {
                        name.set("MIT License")
                        url.set("https://opensource.org/licenses/MIT")
                    }
                }
                developers {
                    developer {
                        id.set("fmjsjx")
                        name.set("MJ Fang")
                        email.set("fmjsjx@163.com")
                        url.set("https://github.com/fmjsjx")
                        organization.set("fmjsjx")
                        organizationUrl.set("https://github.com/fmjsjx")
                    }
                }
                scm {
                    url.set("https://github.com/fmjsjx/myboot")
                    connection.set("scm:git:https://github.com/fmjsjx/myboot.git")
                    developerConnection.set("scm:git:https://github.com/fmjsjx/myboot.git")
                }
            }
    	}
    }
}

signing {
    sign(publishing.publications["mavenJava"])
}
